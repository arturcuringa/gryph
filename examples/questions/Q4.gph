# Import math functions from standard library
use "../library/math.gph"

# Type to represent a rational
Rational_t {
    numerador, denominador : int = 1;
}

# Uses mdc to make a rational irreducible
sub reduce (a : Rational_t&) {
   mdcc : int = 1;
   mdc(a{numerador},a{denominador},mdcc);
   a{numerador} = a{numerador} / mdcc;
   a{denominador} = a{denominador} / mdcc;
}

# Check rationals for equality
sub is_same_rational (a, b : Rational_t) : bool {
    return a{numerador} * b{denominador} == a{denominador} * b{numerador};
}

# Make a valid rational (do not reduce)
sub make_rational (a, b : int) : Rational_t {
    if (b != 0) {
        r : Rational_t = Rational_t {numerador = a, denominador = b}; 
        return r;
    } else {
        print "erro: b não pode valer 0";
        return;
    }
}

# Sum two rationals, producing the reduced version
sub sum (a, b : Rational_t) : Rational_t {
    new_den : int = a{denominador} * b{denominador};
    new_num : int = b{denominador}*a{numerador} + b{numerador}*a{denominador};
    s : Rational_t = make_rational(new_num, new_den);
    reduce(s);
    return s; 
}

# Negate one rational (do not reduce)
sub neg (a : Rational_t) : Rational_t {
    return Rational_t {numerador=-a{numerador},denominador=a{denominador}};
}

# Subtract two rationals, producing the reduced version
sub subt (a, b : Rational_t) : Rational_t {
    return sum(a, neg(b));
}

# Multiply two rationals, producing the reduced version
sub mult (a, b : Rational_t) : Rational_t {
    new_den : int = a{denominador} * b{denominador};
    new_num : int = a{numerador} * b{numerador};
    s : Rational_t = make_rational(new_num, new_den);
    reduce(s);
    return s;
}

# Produce the inverse of a rational (do not reduce)
sub inv (a : Rational_t) : Rational_t {
    if (a{numerador} != 0) {
       return make_rational(a{denominador}, a{numerador});
    } else {
       print "numerador não pode ser zero"; 
    }
}

# Divides two rationals (do not reduce)
sub div (a, b : Rational_t) : Rational_t {
    return mult(a, inv(b));
}

rs : [Rational_t] = [Rational_t{}, Rational_t{}];

s : string;     # Receive the input string
a, b : int;     # Input ints

while (true) {

    inputs : [string] = ["_","_","_","_"];
    
    for i over [0,1] {
        while (b == 0) {
            println inputs[0] + "/" + inputs[1] + ";" + inputs[2] + "/" + inputs[3];
            read s;
            a = s@int;
            read s;
            b = s@int;  
            if (b != 0) {
                rs[i] = make_rational(a, b);
                inputs[2*i] = a@string;
                inputs[2*i + 1] = b@string;
            }
        }
        b = 0;
    } 

    println inputs[0] + "/" + inputs[1] + ";" + inputs[2] + "/" + inputs[3];

    println "(==)    " + is_same_rational(rs[0],rs[1])@string;
    println "(+)     " + sum(rs[0],rs[1])@string;
    println "(-)     " + subt(rs[0],rs[1])@string;
    println "(*)     " + mult(rs[0],rs[1])@string;
    println "(neg)   " + neg(rs[0])@string;
    println "(inv)   " + inv(rs[0])@string;
    println "(/)     " + div(rs[0],rs[1])@string;

    print "De novo? (S/N): ";
    read s;
    if (s != "S") break; 
    
}
