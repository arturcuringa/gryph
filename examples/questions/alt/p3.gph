sub split (original : string; split_char : char) : [string] {
	results : [string] = [""];
	i : int = 0;
	for c over original {
		if (c == split_char) {
			add "" in results;
			i = i + 1;
		} else {
			results[i] = results[i] + c@string;
		}
	}

	return results;
}

sub range(begin : int; end : int; step : int = 1) : [int] {
	l : [int] = [];
	count : int = begin;
	
	while (count < end) {
		add count in l;
		count = count + 1;
	}
	
	return l;
}

sub range(end : int) : [int] {
	return range(0, end);
}

##### END LIB #####

Matrix {
	height, width : int = 0;
	data : [[int]] = [];
}

sub read_matrix() : Matrix {
	input : string;
	row, column : int = 0;
	last_width : int = 0;
	mat : Matrix;

	read input;
	while (input != "") {
		add [] in mat{data};
		nums : [string] = split(input, ' ');
		column = 0;
		for n over nums {
			add n@int in mat{data}[row];
			column = column + 1;
		}

		if (row > 0 and column != last_width) {
			print "Invalid matrix";
			empty : Matrix;
			return empty;
		}
		last_width = column;
		row = row + 1;
		read input;
	}

	mat{height} = row;
	mat{width} = last_width;
	return mat;
}

sub multiply_matrices(m1, m2 : Matrix&) : Matrix {
	result : Matrix;
	if (m1{width} != m2{height}) {
		print "Can't multiply matrices";
		return result;
	}

	for i over range(m1{height}) {
		result_row : [int] = [];
		for j over range(m2{width}) {
			add 0 in result_row;
			for k over range(m1{width}) {
				result_row[j] = result_row[j] +
					(m1{data}[i][k] * m2{data}[k][j]);
			}
		}
		add result_row in result{data};
	}

	result{height} = m1{height};
	result{width} = m2{width};
    return result;
}

sub add_matrices(m1, m2 : Matrix&) : Matrix {
	result : Matrix;
	if (m1{height} != m2{height} or m1{width} != m2{width}) {
		print "Can't add matrices";
		return result;
	}

	for i over range(m1{height}) {
		add
			[m1{data}[i][j] + m2{data}[i][j] for j over range(m1{width})]
		in result{data};
	}

	result{height} = m1{height};
	result{width} = m1{width};
    return result;
}

sub print_matrix(m : Matrix&) {
	print m{height}@string + ", " + m{width}@string;
	for l over m{data}
		print l;
	return 0;
}

print "Input first matrix:";
m1 : Matrix = read_matrix();
if (m1{height} > 0) print_matrix(m1);

print "Input second matrix:";
m2 : Matrix = read_matrix();
if (m2{height} > 0) print_matrix(m2);

print "Sum of matrices:";
r1 : Matrix = add_matrices(m1, m2);
if (r1{height} > 0) print_matrix(r1);

print "Product of matrices:";
r2 : Matrix = multiply_matrices(m1, m2);
if (r2{height} > 0) print_matrix(r2);
